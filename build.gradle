plugins {
  id 'org.ajoberstar.grgit'
  id 'org.ajoberstar.reckon'

  id 'maven-publish'
  id 'java-gradle-plugin'
  id 'java-library'
  id 'groovy'

  id 'com.gradle.plugin-publish'
  id 'org.ajoberstar.stutter'
}

apply from: 'gradle/spotless.gradle'
apply from: 'gradle/locking.gradle'
apply from: 'gradle/bintray.gradle'

reckon {
  scopeFromProp()
  stageFromProp('alpha', 'beta', 'rc', 'final')
}

group = 'org.ajoberstar'

sourceCompatibility = 8

java {
  withSourcesJar()
  withJavadocJar()
}

repositories {
  mavenCentral()
}

// avoid conflict with localGroovy()
configurations.all {
  exclude group: 'org.codehaus.groovy'
}

dependencies {
  compileOnly gradleApi()
  implementation 'com.fasterxml.jackson.core:jackson-databind:latest.release'

  testImplementation 'org.junit.jupiter:junit-jupiter-api:latest.release'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:latest.release'

  compatTestImplementation gradleTestKit()
  compatTestImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

jar {
  manifest {
    attributes 'Automatic-Module-Name': 'org.ajoberstar.stutter'
  }
}

test {
  useJUnitPlatform()
}

stutter {
  sparse = true
  java(8) {
    compatibleRange '4.3'
  }
  java(11) {
    compatibleRange '5.0'
  }
  java(15) {
    compatibleRange '6.3'
  }
}

publishing {
  publications {
    main(MavenPublication) {
      from components.java
    }
  }
}

pluginBundle {
  website = 'https://github.com/ajoberstar/gradle-stutter'
  vcsUrl = 'https://github.com/ajoberstar/gradle-stutter.git'
  description = 'A Gradle plugin plugin'
  plugins {
    publishPlugin {
      id = 'org.ajoberstar.stutter'
      displayName = 'Stutter Plugin'
      tags = ['plugin-plugin', 'testkit', 'testing']
    }
  }
  mavenCoordinates {
    groupId = project.group
    artifactId = project.name
    version = project.version
  }
}

// remove duplicate publication
gradlePlugin.automatedPublishing = false
