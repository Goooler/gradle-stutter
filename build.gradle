plugins {
  id 'org.ajoberstar.defaults' version '0.15.1'
  id 'org.ajoberstar.stutter' version '0.5.1-rc.1'
  id 'groovy'
  id 'java-gradle-plugin'
}

group = 'org.ajoberstar'

sourceCompatibility = 8

repositories {
  jcenter()
}

dependencyLocking {
  lockAllConfigurations()
}

dependencies {
  implementation gradleApi()
  implementation 'com.fasterxml.jackson.core:jackson-databind:latest.release'

  testImplementation 'org.junit.jupiter:junit-jupiter-api:latest.release'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:latest.release'

  compatTestImplementation gradleTestKit()
  compatTestImplementation 'org.spockframework:spock-core:1.2-groovy-2.5'
}

test {
  useJUnitPlatform()
}

stutter {
  sparse = true
  java(8) {
    compatibleRange '4.3'
  }
  java(11) {
    compatibleRange '5.0'
  }
  java(13) {
    compatibleRange '6.0'
  }
}

publishing {
  repositories {
    maven {
      name = 'bintray'
      url = 'https://api.bintray.com/maven/ajoberstar/maven/gradle-stutter/;publish=1'
      credentials {
        username = System.env['BINTRAY_USER']
        password = System.env['BINTRAY_KEY']
      }
    }
  }
}

pluginBundle {
  website = 'https://github.com/ajoberstar/gradle-stutter'
  vcsUrl = 'https://github.com/ajoberstar/gradle-stutter.git'
  description = 'A Gradle plugin plugin'
  plugins {
    publishPlugin {
      id = 'org.ajoberstar.stutter'
      displayName = 'Stutter Plugin'
      tags = ['plugin-plugin', 'testkit', 'testing']
    }
  }
  mavenCoordinates {
    groupId = project.group
    artifactId = project.name
    version = project.version
  }
}
